# Como usar o awk para pesquisar texto em arquivos

O awk percorre todas as linhas (de um arquivo, por exemplo) e para cada linha podemos fazer algumas operações, como filtrar, reformatar e exibir apenas o que precisarmos.

Supondo que você tenha um arquivo chamado logs.txt contendo:
```
07.46.199.184 [28/Sep/2010:04:08:20] "GET /robots.txt HTTP/1.1" 200 0 "msnbot"
123.125.71.19 [28/Sep/2010:04:20:11] "GET / HTTP/1.1" 304 - "Baiduspider"
```

Obs: Cada campo fica separado por espaços ou tabulação. O que existe dentro das aspas NÃO é considerado como um campo único. No exemplo acima existe 8 campos por linha.


## Variáveis especiais

**$0** = a linha completa.

**$1** = o primeiro campo da linha.

**$2** = o segundo campo da linha.

**NF** = o número de campos da linha.

**(NF-2)** = o antepenúltimo campo da linha. Útil se o número de campos for indeterminado. Seria o mesmo que $6, caso a linha tenha 8 campos como no exemplo.

**NR** = o número da linha que está sendo processada.


## Examplos práticos

Listar todo o conteúdo do arquivo:

```
awk '{print $0}' logs.txt
```

Listar somente o primeiro campo (o IP, neste exemplo):

```
awk '{print $1}' logs.txt
```

Listar o número da linha e o IP:

```
awk '{print NR,"-",$1}' logs.txt
```

Listar somente o IP e o status):

```
awk '{print $1, $6}' logs.txt
awk '{print $1, $(NF-2)}' logs.txt
```

Listar somente a data, sem os colchetes os a hora.
O ```BEGIN{FS=":"}``` indica que o separador passa a ser o ```:``` nesse contexto, então imprime o primeiro campo a partir do segundo caractere com substr(para não pegar o colchetes):

```
awk '{print $2}' logs.txt | awk 'BEGIN{FS=":"}{print $1}' | awk '{print substr($1,2)}'
awk '{print $2}' logs.txt | awk 'BEGIN{FS=":"}{print substr($1,2)}'
```

Para cada linha uma variável ```a``` é incrementada com o valor do status (apenas como exemplo):

```
awk '{a+=$(NF-2); print "Soma até o momento:", a}' logs.txt
```
Apenas incrementar os valores do status, e só imprimir a soma no final:

```
awk '{a+=$(NF-2)}END{print "Soma:", a}' logs.txt
```

Imprime a URL da requisição. a parte ```BEGIN{FS="\""}``` é específica pra tirar as aspas. Ela define que o delimitador é a aspas, então pega o segundo campo, que é o que está entre as aspas.

```
awk '{print $3, $4, $5}' logs.txt | awk 'BEGIN{FS="\""}{print $2}'
```

Pesquisa no arquivo a string "texto a ser encontrado". Se encontrar imprime a linha.

```
awk '{if (index($0,"texto a ser encontrado") > 0) print $0}' livro.txt
```

Pesquisa por todos os arquivos .txt do diretório atual, e quando acha uma ou mais ocorrências da string, mostra o nome do arquivo e a linha da ocorrência.

```
awk '{ if (index($0,"texto a ser encontrado") > 0) {if (aa != FILENAME) {aa=FILENAME; print "\n"FILENAME; }; print "   " $0; }; }' *.txt
```

Passando os arquivos encontrados pelo comando find para o awk, permitindo assim uma busca recursiva nos arquivos.

```
find . -type f -name '*.txt' -exec awk '{ if (index($0,"banco") > 0) {if (aa != FILENAME) {aa=FILENAME; print "\n"FILENAME; }; print "   " $0; }; }' {} \;
```

fawk 1.2 - script awk completo para pesquisar texto dentro de arquivos:

```
#!/bin/awk -f

BEGIN {

	# Show help if number of arguments is fewer than 2.
	if (ARGC < 3) {
		print;
		print "fawk 1.2";
		print;
		print "Description:"
		print "	fawk is a script to find text inside files. Easy to use and nice output, get the job done."
		print
		print "Usage:"
		print "	fawk \"<string>\" <files> [-f]"
		print
		print "Options:"
		print "	-f = output only the filenames, not the lines containing the searched string."
		print;
		print "Examples:"
		print "	Search all files containing \"string to search\" inside all php files:"
		print;
		print "		fawk \"string to search\" *.php"
		print;
		print "	Search all files containing \"string to search\" inside all php files, but show only the filenames:"
		print;
		print "		fawk \"string to search\" *.php -f"
		print;
		print "	Search recursively in directories all files containing \"text to search\" inside all txt files:"
		print;
		print "		find . -iname \\*.txt -exec fawk \"text to search\" {} \\;"
		print;
		print "	Search recursively in directories all files containing \"text to search\" inside all txt files, but show only the filenames:"
		print;
		print "		find . -iname \\*.txt -exec fawk \"text to search\" {} -f \\;"
	}

	# Get the string passed as argument to this script.
	search_filter=ARGV[1];

	# Check if "files only" parameter is set.
	files_only=0;
	if (ARGV[ARGC-1] == "-f") {
		files_only = 1;
	}

	# Loop in files
	for (i = 2; i < (ARGC-files_only); i++) {
		NR=1; # line number
		lines_found="";

		# Loop in lines of file
		while(( getline line<ARGV[i]) > 0 ) {
			# Get only lines containing the search string.
			if (index(line, search_filter) > 0) {
				lines_found=lines_found "   {" NR "}" " " line "\n";
			}
			NR++;
  		}

		# Show only filenames if the search string was found
		if (lines_found != "") {
			print ARGV[i];
			if (files_only != 1) {
				print lines_found;
			}
		}
	}
}
```

---

*keywords: awk, parsing, parser, search, text, pesquisa, texto, tutorial*

*references:*

*https://gregable.com/2010/09/why-you-should-know-just-little-awk.html*

*https://www.tutorialspoint.com/awk/awk_built_in_functions.htm*

*https://www.tutorialspoint.com/awk/awk_built_in_variables.htm*

*https://www.linuxquestions.org/questions/programming-9/how-to-read-a-file-inside-awk-874908/*
